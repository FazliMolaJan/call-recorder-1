apply plugin: 'com.android.application'

import com.android.build.OutputFile

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.github.axet.callrecorder"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 178
        versionName "1.6.23"
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(OutputFile.ABI)
            if (abi == null)
                abi = "universal";
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + abi + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
}

task fetch() {
    def f = project.file('src/main/res/raw/surveys.csv')
    def url = 'https://docs.google.com/spreadsheets/d/1C6HuLIk3k4SmAYEuKOnQynGEPM39rjlu90oX7bYbqLk/gviz/tq?gid=1685986704&tqx=out:csv&tq=SELECT+A,B,C,D,H,I,K,L'
    if (!f.exists())
        new URL(url).withInputStream { i -> f.withOutputStream { it << i } }
}

clean.doLast {
    project.file('src/main/res/raw/surveys.csv').delete()
}

preBuild.dependsOn fetch

dependencies {
    implementation 'com.github.axet:android-audio-library:1.0.118' // implementation project(':android-audio-library')
    api "org.apache.commons:commons-csv:1.5"
    testImplementation 'junit:junit:4.12'
}
